{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

() recv_internal() {
}

forall X -> () print(X x) impure { ~dump(x); }

const INF = (1 << 128);
const CELL_SIZE = 1023;

(int) ubitsize(int value) asm "UBITSIZE";

forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";
int bbits(builder b) asm "BBITS";

(tuple, builder) append (tuple t, builder b, int len, int value) asm """
s1 PUSH s3 PUSH BBITS
ADD 1023 INT GREATER
IF:<{
  2 2 BLKSWAP
  CONS
  NEWC
  2 2 BLKSWAP
}>
ROTREV
STUX
""";

;; (builder, tuple) append(builder b, tuple t, int value, int len) inline {
;;   if(b.builder_bits() + len > CELL_SIZE) {
;;     t~push_back(b);
;;     b = begin_cell();
;;   }

;;   b~store_uint(value, len);
;;   return (b, t);
;; }

(cell) get_cell (tuple stack, builder last_builder) inline {
  cell last_cell = last_builder.end_cell();
  while(~ stack.is_null()) {
    builder tb = stack~pop_back();
    last_cell = tb.store_ref(last_cell)
                  .end_cell();
  }
  return last_cell;
}

(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  int flag_len = ubitsize(flag);
  int value_len = ubitsize(value);

  builder answer =  begin_cell();
  tuple stack = null();

  (int linked_list_len, _, _) = linked_list.compute_data_size(INF);
  slice slinked_list = linked_list.begin_parse();
  
  int slide_window = 0;
  int slide_window_len = 0;

  repeat(linked_list_len) {
    repeat(slinked_list.slice_bits()) {
      slide_window = ((slide_window << 1) + slinked_list~load_uint(1)) & ((1 << (flag_len + 1)) - 1);
      slide_window_len += 1;

      if(slide_window_len >= flag_len) {
        if(slide_window_len > flag_len) {
          (stack, answer) = append(
            stack, 
            answer,
            1,
            (slide_window & (1 << flag_len)) >> flag_len);
          slide_window_len -= 1;
        }

        if(flag == (slide_window & ((1 << flag_len) - 1))) {
          slide_window_len = 0;
          (stack, answer) = append(
            stack,
            answer,
            value_len,
            value);
        }
      }
    }

    if(slinked_list.slice_refs()) {
      slinked_list = slinked_list~load_ref().begin_parse();
    }
  }

  (stack, answer) = append(stack, answer, slide_window_len, slide_window & ((1 << slide_window_len) - 1));

  return stack.get_cell(answer);
  ;; return begin_cell().end_cell();
}

